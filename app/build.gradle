import dependencies.Depends
import dependencies.Versions

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply from: "$rootDir/gradle/app-version.gradle"

android {
    compileSdkVersion Versions.androidCompileSdkVersion
    buildFeatures.dataBinding = true

    defaultConfig {
        applicationId "com.konifar.cardinputhelper"
        minSdkVersion Versions.androidMinSdkVersion
        targetSdkVersion Versions.androidTargetSdkVersion
        versionCode project.appVersionCode
        versionName project.appVersionName
    }

    signingConfigs {
        release {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    dexOptions {
        preDexLibraries false
    }

    buildTypes {
        debug {
            debuggable true
            ext.alwaysUpdateBuildId = false
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
        }
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kapt.mapDiagnosticLocations = true
    aaptOptions.cruncherEnabled = false

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            if (System.getenv("CI") as boolean) {
                jvmArgs '-Xmx2048m', '-XX:+HeapDumpOnOutOfMemoryError'
            }
        }
    }
}

dependencies {
    implementation project(':library')

    implementation Depends.Kotlin.stdlib

    // AndroidX
    implementation Depends.AndroidX.appCompat
    implementation Depends.AndroidX.design
    implementation Depends.AndroidX.constraint
    implementation Depends.AndroidX.coreKtx
    implementation Depends.AndroidX.activityKtx
    implementation Depends.AndroidX.fragmentKtx
    implementation Depends.AndroidX.DataBinding.common
    implementation Depends.AndroidX.DataBinding.runtime
    implementation Depends.AndroidX.Lifecycle.liveData
    implementation Depends.AndroidX.Lifecycle.liveDataCoreKtx
    implementation Depends.AndroidX.Lifecycle.liveDataKtx
    implementation Depends.AndroidX.Lifecycle.extension
    implementation Depends.AndroidX.Lifecycle.viewModel

    // Debug
    implementation Depends.Timber.core

    // Test
    testImplementation Depends.Test.junit
    testImplementation Depends.Test.testRunner
    testImplementation Depends.Test.testRules
    testImplementation Depends.Test.testCoreKtx
    androidTestImplementation Depends.Test.androidJunit4Ktx
}